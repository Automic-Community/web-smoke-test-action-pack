*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Suite Setup
Force Tags        POST_SEARCH_TEXT    win
Test Template     Post Search Text Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    URL                                               KEY VALUE                        EXPECTED TITLE       REGEX    USERNAME         PASSWORD      TIMEOUT    IGNORE    RETURN CODE
Invalid username      https://${_HOST}/smoke_web/basicauth/post.aspx    name=world                       Hello world          NO       Adminis          S3rv3r@Aut    10000      YES       3

Invalid password      https://${_HOST}/smoke_web/basicauth/post.aspx    name=world                       Hello world          NO       ${EMPTY}         ${EMPTY}      10000      YES       3

Not ignore            https://${_HOST}/smoke_web/post.aspx              name=world                       Hello world          NO       ${EMPTY}         ${EMPTY}      10000      NO        6

Timeout               http://10.255.255.1                               name=world                       Hello world          NO       Administrator    S3rv3r@Aut    10000      NO        1

Webserver not available
                      http://127.0.0.1:3434/                            name=world, email=ntn@automic    aut[ok]mic$          YES      Administrator    S3rv3r@Aut    10000      YES       2

Expected text not found
                      [Tags]                                            knownbug                         PCK_SMOKE_WEB-1
                      https://${_HOST}/smoke_web/basicauth/post.aspx    name=world                       Hello automic        YES      Administrator    S3rv3r@Aut    10000      YES       10

Text does not match, different space character in the midle of string
                      [Tags]                                            PCK_SMOKE_WEB-1                  knownbug
                      https://${_HOST}/smoke_web/basicauth/post.aspx    name=world                       Hello \ world        NO       Administrator    S3rv3r@Aut    10000      YES       10

Search a regular expression but select Regex
                      [Tags]                                            knownbug                         PCK_SMOKE_WEB-1
                      https://${_HOST}/smoke_web/post.aspx              name=world, email=ntn@automic    au[tu]om..c          YES      ${EMPTY}         ${EMPTY}      10000      YES       10

Text in regular expression not match
                      [Tags]                                            PCK_SMOKE_WEB-1                  knownbug
                      https://${_HOST}/smoke_web/basicauth/post.aspx    name=world, email=ntn@automic    he?llo{4,5}rl[yv]    YES      Administrator    S3rv3r@Aut    10000      YES       10

*** Keywords ***
Post Search Text Template
    [Arguments]    ${url}    ${keyValue}    ${expectedText}    ${regex}    ${username}    ${password}
    ...    ${timeout}    ${ignore}    ${returnCode}
    Init Action    ${ACTION_POST_SEARCH_TEXT}
    Action Set    ${PRT_POST_SEARCH_TEXT_ST_URL}    ${url}
    Action Set    ${PRT_POST_SEARCH_TEXT_ST_KEY_VALUE}    ${keyValue}
    Action Set    ${PRT_POST_SEARCH_TEXT_ST_EXPECTED_TEXT}    ${expectedText}
    Action Set    ${PRT_POST_SEARCH_TEXT_ST_TREAT_AS_REGEX}    ${regex}
    Action Set    ${PRT_POST_SEARCH_TEXT_ST_USERNAME}    ${username}
    Action Set    ${PRT_POST_SEARCH_TEXT_ST_PASSWORD}    ${password}
    Action Set    ${PRT_POST_SEARCH_TEXT_ST_OPEN_TIMEOUT}    ${timeout}
    Action Set    ${PRT_POST_SEARCH_TEXT_ST_IGNORE_SERVER_CERT}    ${ignore}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${returnCode}
