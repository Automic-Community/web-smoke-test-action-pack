*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Suite Setup
Force Tags        GET_SEARCH_TEXT    win    unix
Test Template     Get Search Text Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    URL                                             EXPECTED             REGEX    USERNAME         PASSWORD      TIMEOUT    IGNORE    RETURN CODE
No Proxy Invalid username
                      http://${_HOST}/smoke_web/basicauth/            world                NO       Admin            S3rv3r@Aut    10000      NO        3

No Proxy Invalid password
                      http://${_HOST}/smoke_web/basicauth/            hello                NO       Administrator    S3rv3r        10000      NO        3

No Proxy No Ignore certificate for HTTPS
                      https://${_HOST}/smoke_web/basicauth/           [^t]ello             YES      Administrator    S3rv3r@Aut    10000      NO        6

No Proxy Timeout      http://${_HOST}/smoke_web/timeout/index.aspx    [hc]*w.rld           YES      Administrator    S3rv3r@Aut    1000       NO        1

No Proxy Webserver not available
                      http://127.0.0.1:3434/                          worl[da]             YES      Administrator    S3rv3r@Aut    10000      NO        2

Text not match        https://${_HOST}/smoke_web/                     helloworld           NO       ${EMPTY}         ${EMPTY}      10000      YES       7

No Regex, but input regular expression
                      https://${_HOST}/smoke_web/                     hel{2,3}o |world     NO       ${EMPTY}         ${EMPTY}      10000      YES       7

Text in Regex not match
                      http://${_HOST}/smoke_web/                      he?llo{4,5}rl[yv]    YES      ${EMPTY}         ${EMPTY}      10000      NO        7

Text does not match, different space character in the midle of string
                      https://${_HOST}/smoke_web/basicauth/           hello \ world        NO       Administrator    S3rv3r@Aut    10000      YES       7

*** Keywords ***
Get Search Text Template
    [Arguments]    ${url}    ${expected}    ${regex}    ${username}    ${password}    ${timeout}
    ...    ${ignore}    ${returnCode}
    Init Action    ${ACTION_GET_SEARCH_TEXT}
    Action Set    ${PRT_GET_SEARCH_TEXT_ST_URL}    ${url}
    Action Set    ${PRT_GET_SEARCH_TEXT_ST_EXPECTED_TEXT}    ${expected}
    Action Set    ${PRT_GET_SEARCH_TEXT_ST_TREAT_AS_REGEX}    ${regex}
    Action Set    ${PRT_GET_SEARCH_TEXT_ST_USERNAME}    ${username}
    Action Set    ${PRT_GET_SEARCH_TEXT_ST_PASSWORD}    ${password}
    Action Set    ${PRT_GET_SEARCH_TEXT_ST_OPEN_TIMEOUT}    ${timeout}
    Action Set    ${PRT_GET_SEARCH_TEXT_ST_IGNORE_SERVER_CERT}    ${ignore}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${returnCode}
