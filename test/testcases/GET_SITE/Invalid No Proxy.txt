*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Suite Setup
Force Tags        GET_SEARCH_ELEMENT    win
Test Template     Get Site Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    URL                                             USERNAME         PASSWORD      TIMEOUT    IGNORE    RETURN CODE
Invalid user with proxy
                      https://${_HOST}/smoke_web/basicauth/           Adminis          S3rv3r@Aut    10000      YES       3

Invalid password with proxy
                      https://${_HOST}/smoke_web/basicauth/           Administrator    ${EMPTY}      10000      YES       3

Timeout               #Sites is not existed, or sites are down
                      http://${_HOST}/smoke_web/timeout/index.aspx    Administrator    S3rv3r@Aut    10000      NO        1

Web Server is not available
                      http://127.0.0.1:3434/                          ${EMPTY}         ${EMPTY}      10000      YES       2

Not Ignore certificate for HTTPS sites, with proxy
                      https://${_HOST}/smoke_web/                     ${EMPTY}         ${EMPTY}      10000      NO        6

*** Keywords ***
Get Site Template
    [Arguments]    ${url}    ${username}    ${password}    ${timeout}    ${ignore}    ${returnCode}
    Init Action    ${ACTION_GET_SITE}
    Action Set    ${PRT_GET_SITE_ST_URL}    ${url}
    Action Set    ${PRT_GET_SITE_ST_USERNAME}    ${username}
    Action Set    ${PRT_GET_SITE_ST_PASSWORD}    ${password}
    Action Set    ${PRT_GET_SITE_ST_OPEN_TIMEOUT}    ${timeout}
    Action Set    ${PRT_GET_SITE_ST_IGNORE_SERVER_CERT}    ${ignore}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${returnCode}
    #Action Return Status Should Be    ${returnStatus}
    #Action Return Variable Should Be    &UC4RB_OUT_VARA#    ${outVara}
