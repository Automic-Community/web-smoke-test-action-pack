*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Suite Setup
Force Tags        GET_SEARCH_TITLE    win
Test Template     Get Search Title Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    URL                                      EXPECTED TITLE            REGEX    USERNAME         PASSWORD      TIMEOUT    IGNORE    RETURN CODE
Invalid user          https://${_HOST}/smoke_web/basicauth/    Web smoke test            NO       Adminis          S3rv3r@Aut    10000      YES       3

Invalid password      https://${_HOST}/smoke_web/basicauth/    Web smoke test            NO       Administrator    ${EMPTY}      10000      YES       3

Not ignore            https://${_HOST}/smoke_web/              Web smoke test            NO       ${EMPTY}         ${EMPTY}      10000      NO        6

Timeout               http://10.255.255.1                      Web smoke test            NO       Administrator    S3rv3r@Aut    10000      NO        1

Webserver not available
                      http://127.0.0.1:3434/                   Web [^t]moke test         YES      Administrator    S3rv3r@Aut    60000      YES       2

Title does not match, not complete string
                      https://${_HOST}/smoke_web/basicauth/    Web                       NO       Administrator    S3rv3r@Aut    10000      YES       7

Title does not match, different space character in the midle of string
                      https://${_HOST}/smoke_web/basicauth/    Web \ smoke test \        NO       Administrator    S3rv3r@Aut    10000      YES       7

Title does not match
                      https://${_HOST}/smoke_web/basicauth/    wrong title               NO       Administrator    S3rv3r@Aut    10000      YES       7

Search a regular expression but select Regex
                      https://${_HOST}/smoke_web/              Web smoke{1,3} te?st      NO       ${EMPTY}         ${EMPTY}      10000      YES       7

Title in regular expression not match
                      https://${_HOST}/smoke_web/basicauth/    Web [^t]moke te{2,3}st    YES      Administrator    S3rv3r@Aut    10000      YES       7

*** Keywords ***
Get Search Title Template
    [Arguments]    ${url}    ${title}    ${regex}    ${username}    ${password}    ${timeout}
    ...    ${ignore}    ${returnCode}
    Init Action    ${ACTION_GET_SEARCH_TITLE}
    Action Set    ${PRT_GET_SEARCH_TITLE_ST_URL}    ${url}
    Action Set    ${PRT_GET_SEARCH_TITLE_ST_EXPECTED_TITLE}    ${title}
    Action Set    ${PRT_GET_SEARCH_TITLE_ST_TREAT_AS_REGEX}    ${regex}
    Action Set    ${PRT_GET_SEARCH_TITLE_ST_USERNAME}    ${username}
    Action Set    ${PRT_GET_SEARCH_TITLE_ST_PASSWORD}    ${password}
    Action Set    ${PRT_GET_SEARCH_TITLE_ST_OPEN_TIMEOUT}    ${timeout}
    Action Set    ${PRT_GET_SEARCH_TITLE_ST_IGNORE_SERVER_CERT}    ${ignore}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${returnCode}
